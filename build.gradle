plugins {
    id 'com.gradle.plugin-publish' version '2.0.0'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
    id 'ru.vyarus.quality' version '6.0.1'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'ru.vyarus.java-lib' version '3.0.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.53.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.12"
    id 'ru.vyarus.mkdocs' version '4.0.1'
}

java {
    sourceCompatibility = 1.8
}

wrapper {
    gradleVersion = '8.14.3'
    distributionType = Wrapper.DistributionType.BIN
}

repositories { mavenLocal(); mavenCentral(); gradlePluginPortal() }
dependencies {
    implementation 'org.testcontainers:testcontainers:1.21.3'

    testImplementation('org.spockframework:spock-core:2.4-M6-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'net.bytebuddy:byte-buddy:1.17.7'
    testImplementation 'org.objenesis:objenesis:3.4'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

group = 'ru.vyarus'
description = 'Use python modules in gradle build'

github {
    user = 'xvik'
    license = 'MIT'
}

mkdocs {
    extras = [
            'version': '4.1.0',
            'image': 'python:3.12.7-alpine3.20'
    ]
    publish {
        docPath = mkdocs.extras['version']
        rootRedirect = true
        rootRedirectTo = 'latest'
        versionAliases = ['latest']
        hideOldBugfixVersions = true
    }
}

maven.pom {
    developers {
        developer {
            id = 'xvik'
            name = 'Vyacheslav Rusakov'
            email = 'vyarus@gmail.com'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign).configureEach { onlyIf { !System.getenv('JITPACK') } }

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

javaLib {
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}


gradlePlugin {
    plugins {
        usePythonPlugin {
            id = 'ru.vyarus.use-python'
            displayName = project.description
            description = 'Manage pip dependencies and use python in gradle build'
            tags.set(['python', 'virtualenv'])
            implementationClass = 'ru.vyarus.gradle.plugin.python.PythonPlugin'
        }
    }
}

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = [
            'publishMavenPublicationToSonatypeRepository',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    maxHeapSize = '2g'
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
