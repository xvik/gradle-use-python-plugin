plugins {
    id 'com.gradle.plugin-publish' version '0.21.0'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '3.0.2'
    id 'ru.vyarus.quality' version '4.9.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
    id 'ru.vyarus.java-lib' version '2.4.0'
    id 'ru.vyarus.github-info' version '1.5.0'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.12"
    id 'ru.vyarus.mkdocs' version '3.0.0'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '6.9'
}

repositories { mavenLocal(); mavenCentral(); gradlePluginPortal() }
dependencies {
    implementation ('org.testcontainers:testcontainers:1.18.3')

    testImplementation('org.spockframework:spock-core:2.0-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation 'net.bytebuddy:byte-buddy:1.14.5'
    testImplementation 'org.objenesis:objenesis:3.3'
}

group = 'ru.vyarus'
description = 'Use python modules in gradle build'

github {
    user 'xvik'
    license 'MIT'
}

mkdocs {
    extras = [
            'version': '3.0.0',
            'image': 'python:3.10.8-alpine3.15'
    ]
    publish {
        docPath = mkdocs.extras['version']
        rootRedirect = true
        rootRedirectTo = 'latest'
        versionAliases = ['latest']
    }
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign) {onlyIf { !System.getenv('JITPACK') }}

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

javaLib {
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}


gradlePlugin {
    plugins {
        usePythonPlugin {
            id = 'ru.vyarus.use-python'
            displayName = project.description
            implementationClass = 'ru.vyarus.gradle.plugin.python.PythonPlugin'
        }
    }
}
pluginBundle {
    description = 'Manage pip dependencies and use python in gradle build'
    tags = ['python', 'virtualenv']

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = [
            'publishMavenPublicationToSonatypeRepository',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat 'full'
    }
    maxHeapSize = '512m'
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
